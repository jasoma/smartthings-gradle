buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id 'java'
    id 'groovy'
    id 'application'
    id 'com.github.tkruse.groovysh' version '1.0.8'
    id 'com.github.johnrengelman.shadow' version '1.2.2'
}

group 'com.github.jasoma'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

mainClassName = 'com.github.jasoma.stsync.cli.Main'

repositories {
    mavenCentral()
}

dependencies {
    compile localGroovy()

    compile 'org.jsoup:jsoup:1.8.3'
    compile 'commons-cli:commons-cli:1.3.1'

    testCompile 'org.spockframework:spock-core:1.0-groovy-2.3'
    testRuntime "cglib:cglib-nodep:3.1"
    testRuntime "org.objenesis:objenesis:2.1"
}

test {
    testLogging.events 'passed', 'skipped', 'failed'
    testLogging.exceptionFormat = 'full'
    testLogging.showStandardStreams = true

    exclude '**/integration/*'
}

task cleanTestApps(type: Delete) {
    delete 'build/tmp/SyncTestApp'
    delete 'build/tmp/SyncTestDevice'
}

task integration(type: Test, dependsOn: ['shadowJar', 'cleanTestApps']) {
    testLogging.events 'passed', 'skipped', 'failed'
    testLogging.exceptionFormat = 'full'
    testLogging.showStandardStreams = true

    include '**/integration/*'
    environment(
            username: project.property('smartthings.username'),
            password: project.property('smartthings.password'),
            'stsync.jar': shadowJar.archivePath)
}

build.dependsOn << integration
